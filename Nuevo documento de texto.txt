USE [dbBDC_Statement]
GO
/****** Object:  StoredProcedure [dbo].[SpInsertUnifiedMonthlyPetition]    Script Date: 18/12/2023 9:48:43 a. m. ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-----------------------------------------------------------------------------
-- Nombre del Objeto:   SpInsertUnifiedMonthlyPetition
-- Autor:               Beatriz Consuegra
-- Descripción:         Inserta la petición mensual del proceso unificado. 
-- Fecha Creación:      Diciembre 14 de 2023

-- Modificado Por:		
-- Fecha Modificación:  
-- Descripción:	

--Ejemplo de llamado
--exec dbo.SpInsertUnifiedMonthlyPetition @ID_BUILD_PETITION=256762,@MARKETS=N'cad_codigos_marcado',@DT_MONTH='2023-10-01 00:00:00',@NR_PROCESSED_ACCOUNTS=0,@NR_GENERATED_FILES=0,@NR_TOTAL_ACCOUNTS=NULL,@NR_CURRENT_ACCOUNT=NULL,@DT_LAST_MODIFICATION='2023-12-20 09:10:26.120',@CD_FORMAT=N'pdf,xlsx',@CUENTAS=N'133668,137590',@CUENTASOYD=N'59243,59241,59240,59220,59210,53250,61079,61076,61074,61073,59258,61071,61070,59257,61069,61068,59248,37389,47712,44077,53284,37384,47710,44075,47708,53278,37370,47703,44067,37365,53275,47687,44054,44047,47686,44025,53261,44023,47682,53257,61082,44020,53255,61080,44008,26639,57804,63770,62409,26612,57803,50425,26607,63765,57792,26601,37442,26590,50424,57791,26589,50418,37438,26585,37430,47734,50417,53312,26499,47723,53311,50415,53300,37408,50414,47720,50413,37401,53298,44079,55877,62428,41801,55874,41790,62427,55872,41788,57830,41783,63795,57829,41764,62421,63792,41761,63790,62420,41760,63786,62419,57820,50433,26708,41759,63778,62418,41746,50432,57817,63777,41744,62414,26668,50430,57805,63775,62412,41743,31948,31939,31938,55913,55911,55909,55906,55904,55895,55894,62429,55890,34458,32023,34455,32021,31981,34453,31976,34446,31965,34443,31963,34436,34498,34494,34489,34459'
--------------------------------------------------------------------------------------
CREATE OR ALTER   PROCEDURE [dbo].[SpInsertUnifiedMonthlyPetition] 
	@ID_BUILD_PETITION INT,
    @MARKETS VARCHAR(MAX),
    @DT_MONTH DATE,
    @NR_PROCESSED_ACCOUNTS INT,
    @NR_GENERATED_FILES INT,
    @NR_TOTAL_ACCOUNTS INT,
    @NR_CURRENT_ACCOUNT INT,
    @DT_LAST_MODIFICATION DATETIME,
	@CD_FORMAT VARCHAR(10),
	@CUENTAS VARCHAR(MAX),
	@CUENTASOYD VARCHAR(MAX)
AS
BEGIN
	BEGIN TRY
		DECLARE @CUENTAS_HOMOLOGADAS VARCHAR(MAX);
		
		IF (ISNULL(@CUENTASOYD,'0') <> '0')
		BEGIN
			CREATE TABLE #TEMP_CUENTAS_OYD
			(
				Id INT,
				Valor VARCHAR(30)
			);
			DECLARE @CONT INT = 1,
					@CANT_REGISTROS INT = 0,
					@CUENTA_OYD AS VARCHAR(20);

			INSERT INTO #TEMP_CUENTAS_OYD
			SELECT	*
			FROM	DBO.Split(@CUENTASOYD, ',');

			SELECT	@CUENTAS_HOMOLOGADAS = STRING_AGG(ID_ACCOUNT, ',')
			FROM	dbBDC.LE.TB_ACCOUNT A
			INNER JOIN #TEMP_CUENTAS_OYD TC ON TC.Valor = LTRIM(RTRIM(A.ID_ACCOUNT_SOURCE));

			IF(ISNULL(@CUENTAS,'') <> '')
			BEGIN
				SET @CUENTAS = @CUENTAS + ',' +  @CUENTAS_HOMOLOGADAS;
			END
			ELSE
			BEGIN
				SET @CUENTAS = @CUENTAS_HOMOLOGADAS;
			END
		END

		SELECT	@MARKETS = STRING_AGG(ID_APPROVAL_MARKET, ',')
		FROM	TB_APPROVAL_MARKET_MONTH
		WHERE	DT_APPROVAL_MONTH = @DT_MONTH
		ORDER BY ID_APPROVAL_MARKET ASC

		INSERT INTO [dbo].[TB_MONTHLY_PETITION]
			   ([ID_BUILD_PETITION]
			   ,[MARKETS]
			   ,[DT_MONTH]
			   ,[NR_PROCESSED_ACCOUNTS]
			   ,[NR_GENERATED_FILES]
			   ,[NR_TOTAL_ACCOUNTS]
			   ,[NR_CURRENT_ACCOUNT]
			   ,[DT_LAST_MODIFICATION]
			   ,[CD_FORMAT]
			   ,[CUENTAS])
		 VALUES
			   (@ID_BUILD_PETITION
			   ,@MARKETS
			   ,@DT_MONTH
			   ,@NR_PROCESSED_ACCOUNTS
			   ,@NR_GENERATED_FILES
			   ,@NR_TOTAL_ACCOUNTS
			   ,@NR_CURRENT_ACCOUNT
			   ,@DT_LAST_MODIFICATION
			   ,@CD_FORMAT
			   ,@CUENTAS);

		DECLARE @id_monthly_petition INT;
		SET @id_monthly_petition = SCOPE_IDENTITY();

		IF(ISNULL(@CUENTAS, '') <> '')
		BEGIN
			DECLARE @TOTAL_CUENTAS INT=0;
			SET @TOTAL_CUENTAS = (SELECT (LEN(@CUENTAS) - DATALENGTH(REPLACE(@CUENTAS, ',', ''))) / LEN(',') + 1);
			UPDATE	[dbo].[TB_MONTHLY_PETITION]
			SET		[NR_TOTAL_ACCOUNTS] = @TOTAL_CUENTAS
			WHERE	[ID_MONTHLY_PETITION] = @id_monthly_petition;
		END

		SELECT	ID_MONTHLY_PETITION 
		FROM	[dbo].[TB_MONTHLY_PETITION] 
		WHERE	ID_MONTHLY_PETITION = @id_monthly_petition;

		DROP TABLE IF EXISTS #TEMP_CUENTAS_OYD;
	END TRY
	BEGIN CATCH	
		INSERT INTO [dbo].[TB_ERRORS]
			   ([ID_ERROR_NUMBER],[NM_ERROR_PROCEDURE],[ID_ERROR_LINE],[NM_ERROR_MESSAGE])
		 VALUES
			   (ERROR_NUMBER(),ERROR_PROCEDURE(),ERROR_LINE(),ERROR_MESSAGE());
		
		 DECLARE @strMsg VARCHAR(500);
	
		 SET @strMsg = 'Error en el procedimiento ' + ERROR_PROCEDURE();
		 SET @strMsg = @strMsg + CHAR(13) + 
					  ' - Nro. Error ' + CONVERT(VARCHAR, ERROR_NUMBER())  + CHAR(13) +
					  ERROR_MESSAGE();
		RAISERROR(@strMsg, 16, 1);
	END CATCH
END
